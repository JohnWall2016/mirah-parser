# Copyright (c) 2010 The Mirah project authors. All Rights Reserved.
# All contributing project authors may be found in the NOTICE file.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package mirahparser.impl;

import java.io.BufferedReader
import java.io.InputStreamReader
import java.io.InputStream
import java.io.FileInputStream
import java.util.ArrayList
import java.util.List
import java.util.LinkedList
import java.util.Collections
import java.util.EnumSet
import mmeta.BaseParser.Token

parser MirahParser {
  rlistOf r=_: h=apply(r) t=(comma apply(r))* { [h] + t };
  listOf e=_: rlistOf(e)
              | empty                                               { ArrayList.new } ;
  nosp: t=&lex ?{Token(t).startpos == _start};

  start: ! prog=compstmt $EOF -> ^(Script prog);

  compstmt scope(@cond)
          : UNCOND
            terms? a=(s=statement terms {s})*
            b=statement?
            {addToBlock(a, b)};

  bodystmt: b=compstmt r=Rescue? e=Else? f=Ensure? {
    node = b
    if r != nil
      node = build_node('Rescue', [b, r, e], _start, _pos)
    elsif e != nil
      # Warning?
      node = addToBlock(b, e)
    end
    unless f.nil?
      end_pos = Ast(f).end_position
      node ||= @nil
      node = build_node('Ensure', [node, f], _start, _pos)
    end
    node || @nil
  };

  term: ($NL|$HereDocBegin)=>nl
      | $Semi;
  terms: ( ($NL|$HereDocBegin)=>nl
         | $Semi
         )+;

  $Memo[String]
  nl: $NL {"\n"}
    | heredoc;

  $Memo[Ast]
  statement: f=statement2
             ( ($If)=> $If c=value(expr) f=-> ^(If c f nil)
             | ($Unless)=> $Unless c=value(expr) f=-> ^(If c nil f)
             | ($While)=> $While c=value(expr) f=-> ^(WhileMod c f)
             | ($Until)=> $Until c=value(expr) f=-> ^(UntilMod c f)
             | ($Rescue)=> $Rescue r=statement f=-> ^(Rescue f [^(RescueClause [] nil r)] nil)
             )* {f};
  statement2: ( (($ClassVar|$InstVar|$CONSTANT|$IDENTIFIER) $AndEq)=>
                l=var_lhs $AndEq opt_nl r=command_call
                b=assign(l, r)
                -> ^(If gettable2(l) b gettable2(l))
              | (($ClassVar|$InstVar|$CONSTANT|$IDENTIFIER) $OrEq)=>
                l=var_lhs $OrEq opt_nl r=command_call
                b=assign(l, r)
                -> ^(If gettable2(l) gettable2(l) b)
              | (($ClassVar|$InstVar|$CONSTANT|$IDENTIFIER) $OpAssign)=>
                l=var_lhs op=op_assign r=command_call
                b= -> ^(Call op gettable2(l) [r])
                assign(l, b)
              | ( l=lhs $EQ opt_nl r=command_call assign(l, r)
                | t=value(primary) (($LBrack)=> $LBrack opt_nl a=call_args? opt_nl $RBrack op=op_assign v=command_call
                                                op_attr_assign(t, "[]", a, op, v)
                                   | ($Colons)=> colons f=identifier op=op_assign v=command_call
                                                 op_attr_assign(t, f, [], op, v)
                                   | dot f=(constant | identifier) op=op_assign v=command_call
                                     op_attr_assign(t, f, [], op, v)
                                   )
              )
            | expr
            );

  value v=_ : {checkExpression(v)};

  $Memo[Ast]
  expr: a=( ($Not)=> $Not opt_nl e=expr -> ^(Not e)
          | ($Bang)=> ($Bang e=command_call -> ^(Not e) | arg12)
          | ( c=variable $Question opt_nl t=arg opt_nl $Colon opt_nl e=arg -> ^(If c t e)
            | command_call
            | arg
            )
          )
        ( ($And)=> $And opt_nl b=expr a=-> ^(And a b)
        | ($Or)=> $Or opt_nl b=expr a=-> ^(Or a b))* {a};

  # Node:command - fcall/call/yield/super [!null]
  $Memo[Ast]
  command: ($Super)=> $Super a=command_args -> ^(Super a)
         | ($Yield)=> $Yield a=command_args -> ^(Yield a)
         | (op=operation a=command_args b=cmd_brace_block?-> ^(FCall op a b)
           | t=value(primary) dot_or_colon op=operation2 a=command_args b=cmd_brace_block?-> ^(Call op t a b)
           );

  # Node:command - call with or without block on end [!null]
  command_call scope(@BEG)
              : block_command
              | command
              | $Return BEG a=call_args -> ^(Return a+);
              # | $Break a=call_args -> ^(Break a+)
              # | $Next a=call_args -> ^(Next a+)

  # Node:block_command - A call with a block (foo.bar {...}, foo::bar {...}, bar {...}) [!null]
  block_command: h=block_call (dot_or_colon op=operation2 a=command_args
                               h=-> ^(Call op h a))* {h};
  $Memo[Ast]
  primary: h=value(primary2) ( (dot_or_colon $LParen)=> dot_or_colon a=paren_args iter=brace_block? h=-> ^(Call "call" h a iter)
                             | (dot)=> dot op=operation2 a=opt_paren_args iter=brace_block? h=-> ^(Call op h a iter)
                             | (colons)=> colons (op=operation2 a=paren_args iter=brace_block? h=-> ^(Call op h a iter)
                                                  |b=constant ~(paren_args|call_args|$EQ..$OpAssign) h=-> ^(Colon2Const h b)
                                                  |op=operation3 ~($EQ..$OpAssign) iter=brace_block? h=-> ^(Call op h iter)
                                                  )
                             | ($LBrack)=> $LBrack opt_nl a=call_args? opt_nl $RBrack iter=brace_block? ~($EQ..$OpAssign) h=-> ^(Call "[]" h a iter)
                             )* {h};

  primary2: ($Super)=> primary2_super
          | ($LParen)=> primary2_lparen
          | ($Begin)=> primary2_begin
          | ($LBrack)=> primary2_lbrack
          | ($LBrace)=> primary2_lbrace
          | ($Yield)=> primary2_yield
          | ($Bang)=> primary2_bang
          | ($If)=> primary2_if
          | ($Unless)=> primary2_unless
          | ($While)=> primary2_while
          | ($Until)=> primary2_until
          | ($For)=> primary2_for
          | ($Class)=> primary2_class
          | ($Def)=> primary2_def
          | ($Break)=> primary2_break
          | ($Next)=> primary2_next
          | ($Redo)=> primary2_redo
          | ($Retry)=> primary2_retry
          | ($Return)=> primary2_return
          | ($DQuote|$SQuote|$Character|$HereDocId)=> string
          | ($RegexBegin)=> regexp
          | ($Backtick)=> unquote
          | primary2_id
          ;
  primary2_super
         : $Super ( a=paren_args iter=brace_block? -> ^(Super a iter)
                  | iter=brace_block -> ^(Super [] iter)
                  | -> ^(ZSuper)
                  );
  primary2_lparen
         : $LParen opt_nl e=compstmt opt_nl $RParen {
           unless e.nil?
             ae = Ast(e)
             ae.start_position = pos(_start)
             ae.end_position = pos(_pos)
           end
           e
         };
  primary2_begin
         : t=$Begin s=block_start(t)! b=bodystmt verify_end(s)! $End -> ^(Begin b);
  primary2_lbrack
         : isBEG $LBrack opt_nl $RBrack -> ^(ZArray)
         | isBEG $LBrack opt_nl a=args trailer opt_nl $RBrack -> ^(Array a+);
  primary2_lbrace scope(@BEG)
         : $LBrace opt_nl ( $RBrace -> ^(Hash)
                          | a=listOf(:assoc) trailer opt_nl $RBrace -> ^(Hash a+)
                          );
  primary2_yield
         : $Yield ( $LParen opt_nl ( $RParen -> ^(Yield)
                                   | a=call_args opt_nl $RParen -> ^(Yield a+)
                                   )
                  | -> ^(Yield)
                  );
  primary2_bang
         : $Bang $LParen opt_nl ($RParen -> ^(Not ^(Nil))
                                | e=value(expr) opt_nl $RParen -> ^(Not e)
                                );
  primary2_if
         : isBEG2 t=$If s=block_start(t)! opt_nl cond=value(expr)!
           Then! body=compstmt! tail=if_tail verify_end(s)! $End
           -> ^(If cond body tail);
  primary2_unless
         : isBEG2 t=$Unless s=block_start(t)! opt_nl cond=value(expr)!
           Then! body=compstmt! tail=Else? verify_end(s)! $End
           -> ^(If cond tail body);
  primary2_while scope(@cond)
         : isBEG2 t=$While s=block_start(t)! opt_nl COND cond=value(expr)!
           Do! body=compstmt! verify_end(s)! $End {body ||= @nil}
           -> ^(While cond body);
  primary2_until scope(@cond)
         : isBEG2 t=$Until s=block_start(t)! opt_nl COND cond=value(expr)!
           Do! body=compstmt! verify_end(s)! $End {body ||= @nil}
           -> ^(Until cond body);
  primary2_for scope(@cond)
         : t=$For s=block_start(t)! opt_nl var=variable! $In opt_nl COND!
           list=value(expr)! Do! body=compstmt verify_end(s)! $End
           -> ^(For var body list);
  primary2_class scope(@BEG)
         : t=$Class sp=block_start(t)!
           ( $LShift! BEG e=expr!  # Should this just allow $Self?
             term! b=compstmt! verify_end(sp)! $End {b ||= @nil}
             -> ^(SClass e b)
           | n=cpath! s=superclass!
             b=compstmt!  # JRuby has bodystmt, but that doesn't make much
                          # sense for mirah. Maybe rescuing exceptions
                          # in the static initializer?
             verify_end(sp)! $End {b ||= @nil}
             -> ^(Class n b s)
           );
  primary2_def scope(@BEG)
         : t=$Def s=block_start(t)!
           ( $Self! dot_or_colon! name=fname!
             args=f_arglist! body=bodystmt! verify_end(s)! $End
             l={ [name] + args + [body] }
             -> ^(DefStatic l+)
           | name=fname! args=f_arglist! body=bodystmt! verify_end(s)! $End
             l={ [name] + args + [body] }
             -> ^(Def l+)
           );
  primary2_break: $Break -> ^(Break);
  primary2_next: $Next -> ^(Next);
  primary2_redo: $Redo -> ^(Redo);
  primary2_retry: $Retry -> ^(Retry);
  primary2_return: $Return -> ^(Return);
  primary2_id: ($Colons)=> colon3 c=constant ~($EQ..$OpAssign) -> ^(Colon3 c)
             | ($Integer)=> fixnum
             | ($Float)=> floatLiteral
             | ($Colon)=> symbol
             | (($FID|$CONSTANT|$IDENTIFIER) ($LParen|$LBrace|$Do))=>
               ( op=operation ( ($LParen)=>
                                a=paren_args iter=brace_block? -> ^(FCall op a iter)
                              | ($LBrace|$Do)=>
                                iter=brace_block -> ^(FCall op [] iter)
                              )
               | variable
               )
             | variable;

  f_arglist: $LParen! opt_nl a=f_args opt_nl $RParen ($Colon type=primary)? BEG {[a, type]}
           | a=f_args ($Colon type=primary)? term {[a, type]};

  $Memo
  comma: $Comma opt_nl;
  f_args: req=f_arg! opt=(comma f_optarg)? rest=(comma f_rest_arg)? req2=(comma f_arg)? block=(comma f_block_arg)?
            -> ^(Arguments req opt rest req2 block)
        | opt=f_optarg! rest=(comma f_rest_arg)? req=(comma f_arg)? block=(comma f_block_arg)?
            -> ^(Arguments nil opt rest req block)
        | rest=f_rest_arg! req=(comma f_arg)? block=(comma f_block_arg)?
            -> ^(Arguments nil nil rest req block)
        | block=f_block_arg -> ^(Arguments nil nil nil nil block)
        | -> ^(Arguments nil nil nil nil nil);

  f_bad_arg: &constant !"formal argument not a constant" ?{false}
           | &ivar !"formal argument not an instance variable" ?{false}
           | &cvar !"formal argument not a class variable" ?{false};

  $Memo[List]
  f_norm_arg: f_bad_arg
            | l=label BEG t=value(primary) n={List(l).get(1)} { [n, t] }
            | n=unquote ($Colon BEG t=value(primary))? { [n, t] }
            | n=identifier { [n, nil] };

  f_arg_item: ~f_opt n=f_norm_arg -> ^(RequiredArgument n+);
            # | $LParen f_margs $RParen

  f_arg: rlistOf(:f_arg_item);

  f_opt: n=f_norm_arg $EQ! opt_nl v=arg_value l={ n + [v] }-> ^(OptArg l+);
  f_optarg: rlistOf(:f_opt);

  f_rest_arg: $Star n=f_norm_arg -> ^(RestArg n+)
            | $Star -> ^(UnnamedRestArg);

  f_block_arg: $Amper n=f_norm_arg $EQ opt_nl !"nil" $Nil -> ^(OptBlockArg n+)
             | $Amper! n=f_norm_arg -> ^(BlockArg n+);

  block_arg: isBEG $Amper ~sp v=arg_value -> ^(BlockPass v);

  # :brace_block - [!null]
  cmd_brace_block: brace_block;
  brace_block scope(@BEG)
             : $LBrace opt_nl (p=block_param !"statement" b=compstmt opt_nl $RBrace
                              |p={nil} b=compstmt opt_nl $RBrace) -> ^(Iter p b)
             | do_block;

  do_block scope(@BEG): ~?{@cond} t=$Do s=block_start(t)! opt_nl p=block_param?! b=compstmt verify_end(s)! $End -> ^(Iter p b);

  # Do we need bv_decls?
  block_param: $Pipe opt_nl a=f_args opt_nl $Pipe BEG {a}
             | $Pipes BEG -> ^(Arguments nil nil nil nil nil);
  block_call: c=command b=do_block
              h={n=Ast(Ast(c).clone); n.add(b);n}
              (dot_or_colon op=operation2 a=opt_paren_args h=-> ^(Call op h a))* {h};

  paren_args: $LParen! opt_nl c=call_args? opt_nl $RParen {c.nil? ? Object(ArrayList.new) : c};
  opt_paren_args: ($EOF)=> {List(nil)}
                | ($LParen)=> paren_args
                | ~(call_args | $EQ..$OpAssign) { List(nil) };

  maybe_rescue: a=arg $Rescue b=value(arg)
                -> ^(Rescue a [^(RescueClause [] nil b)] nil)
              | arg;

  assign lhs=_ rhs=_ : { n=Ast(Ast(lhs).clone); n.add(rhs); n };

  $Memo[Ast]
  arg scope(@BEG): h=arg0 ($Question opt_nl t=arg opt_nl $Colon opt_nl e=arg
                           h=-> ^(If h t e))* {h};
  arg0: ( (($ClassVar|$InstVar|$CONSTANT|$IDENTIFIER) $AndEq)=>
          l=var_lhs $AndEq opt_nl r=arg
          b=assign(l, r)
          -> ^(If gettable2(l) b gettable2(l))
        | (($ClassVar|$InstVar|$CONSTANT|$IDENTIFIER) $OrEq)=>
          l=var_lhs $OrEq opt_nl r=arg
          b=assign(l, r)
          -> ^(If gettable2(l) gettable2(l) b)
        | (($ClassVar|$InstVar|$CONSTANT|$IDENTIFIER) $OpAssign)=>
          l=var_lhs op=assignment_op r=arg
          b= -> ^(Call op gettable2(l) [r])
          assign(l, b)
        | ( l=lhs $EQ opt_nl r=maybe_rescue assign(l, r)
          | t=value(primary) ($LBrack opt_nl a=call_args? opt_nl $RBrack op=assignment_op v=arg
                               op_attr_assign(t, "[]", a, op, v)
                             |colons constant assignment_op arg
                               !"constant re-assignment prohibited" ?{false}
                             |dot_or_colon f=(constant | identifier) op=assignment_op v=arg
                              op_attr_assign(t, f, [], op, v)
                             )
          | colon3 constant assignment_op arg
             !"constant re-assignment prohibited" ?{false}
          | arg2
          )
        );

  arg2: &($Bang..$Tilde|primary)
        a=arg3 t=($Pipes opt_nl BEG b=arg3 a=-> ^(Or a b))* {a};
  arg3: a=arg4 t=($Ampers opt_nl BEG b=arg4 a=-> ^(And a b))* {a};
  arg4: a=arg5 t=(tok=($Match..$EEEQ) op={Token(tok).text} opt_nl BEG b=arg5
                   a=-> ^(Call op a [b])
                 |$NMatch opt_nl BEG b=arg5 a=-> ^(Not ^(Call "=~" a [b])))* {a};
  arg5: a=arg6 t=($Pipe opt_nl BEG b=arg6 a=-> ^(Call "|" a [b])
                 |$Caret opt_nl BEG b=arg6 a=-> ^(Call "^" a [b]))* {a};
  arg6: a=arg7 t=($Amper opt_nl BEG b=arg7 a=-> ^(Call "&" a [b]))* {a};
  arg7: a=arg8 t=(tok=($LShift..$RShift) op={Token(tok).text} opt_nl BEG b=arg8 a=-> ^(Call op a [b]))* {a};
  arg8: a=arg9 t=($Plus opt_nl BEG b=arg9 a=-> ^(Call "+" a [b])
                 |$Minus opt_nl BEG b=arg9 a=-> ^(Call "-" a [b]))* {a};
  arg9: a=arg10 t=(tok=($Star|$Slash|$Percent) op={Token(tok).text} opt_nl BEG b=arg10 a=-> ^(Call op a [b]))* {a};
  arg10: uminus BEG v=arg11 ( ?{Ast(v).name.equals('Fixnum')} -> ^(Fixnum negateFixnum(v))
                            | ?{Ast(v).name.equals('Float')} -> ^(Float negateFloat(v))
                            | -> ^(Call "-@" v)
                            )
       | arg11;
  arg11: a=arg12 $Stars opt_nl BEG b=arg11 -> ^(Call "**" a [b])
       | arg12;
  $Memo[Ast]
  arg12: $Tilde opt_nl BEG v=arg12 -> ^(Call "~" v)
       | $Bang opt_nl BEG v=arg12 -> ^(Not v)
       | uplus BEG v=arg12 -> ^(Call "+@" v)
       | value(primary);

  arg_value: a=arg {checkExpression(a)};

  trailer: ($Comma | nl)? {Object(nil)};
  $Memo
  operation: ($FID)=>fid
           | ($CONSTANT)=>constant
           | ($IDENTIFIER|lastdot)=> identifier
           | ($Backtick)=> unquote;
  $Memo
  operation2: ($FID)=>fid
            | ($CONSTANT)=>constant
            | ($IDENTIFIER)=>identifier
            | ($Backtick)=> unquote
            | (lastdot)=> (identifier | op)
            | op;
  $Memo
  operation3: ($FID)=>fid
            | ($IDENTIFIER)=>identifier
            | ($Backtick)=> unquote
            | (lastdot)=> (identifier | op)
            | op;
  dot: opt_nl $Dot opt_nl;
  colons: $Colons opt_nl;
  dot_or_colon: dot | colons;
  opt_nl: ($NL|$HereDocBegin)=>nl+|;

  # This marks the beginning of an expression. It must be used inside
  # a rule with scope(@BEG).
  BEG: {@BEG = _pos;nil};
  isBEG: (isBEG2 | &oneOf(" \t\f\r\v")) {nil};
  isBEG2: ?{_pos == @BEG}
        | ?{Integer.valueOf(0).equals(col())}
        | ?{@begchars.indexOf(_rpeek) > 0};
  COND: {@cond = true;nil};
  UNCOND: {@cond = false;nil};

  colon3: isBEG $Colons opt_nl;
  uminus: isBEG2 $Minus  opt_nl;
  uplus: isBEG2 $Plus opt_nl;

  one_arg: v=arg_value ~$Rocket {v};
  args: l=rlistOf(:one_arg);
      #  (comma s=splat {l+s} | { l })
      #| splat;

  call_assocs scope(@BEG): l=rlistOf(:assoc) -> ^(Hash l+);
  call_args: c=command { [c] }
           | a=args h=(comma call_assocs)? b=(comma! block_arg)? l={ a }
             ( ?{h} l={ a + [h] } )?
             ( ?{b} l={ l + [b] } )?
             { l }
           | h=call_assocs b=(comma! block_arg)? l={ [h] }
             ( ?{b} l={l+[b]} )?
             { l }
           | b=block_arg { [b] };
  command_args scope(@cond): ~$LParen COND call_args;

  op_assign: t=$AndEq..$OpAssign opt_nl {text=Token(t).text;text.substring(0, text.length - 1)};
  assignment_op: op_assign | $EQ opt_nl {"="};

  $Memo[Ast]
  lhs: ($Colons $CONSTANT)=> colon3 n=constant -> ^(ConstAssign ^(Colon3 n))
     | (($ClassVar|$InstVar|$CONSTANT|$IDENTIFIER) $EQ)=> var_lhs
     | ($Backtick|$ClassVarBacktick|$InstVarBacktick)=> (var_lhs | lhs2)
     | lhs2;
  lhs2: t=value(primary) (($LBrack)=> $LBrack opt_nl k=call_args? opt_nl $RBrack -> ^(AttrAssign "[]=" t k)
                         | dot_or_colon (($CONSTANT)=> n=constant -> ^(ConstAssign ^(Colon2Const t n))
                                        |n=identifier l={ArrayList.new} -> ^(AttrAssign "#{n}=" t l)
                                        )
                         );

  var_lhs: ($ClassVar|$ClassVarBacktick)=> n=cvar -> ^(ClassVarAssign n)
         | ($InstVar|$InstVarBacktick)=> n=ivar -> ^(InstVarAssign n)
         | ($CONSTANT)=> n=constant -> ^(ConstAssign n)
         | ($IDENTIFIER)=> n=identifier -> ^(LocalAssign n)
         | ($Backtick)=> u=unquote i={List(u).get(1)} -> ^(UnquoteAssign i);

  assoc: a=arg_value $Rocket! opt_nl BEG b=arg_value -> ^(Assoc a b)
       | l=label BEG v=arg_value -> ^(Assoc l v);

  label: n=identifier2 nosp $Colon opt_nl -> ^(Symbol n);

  constant: t=$CONSTANT ~(nosp $Colon) {Token(t).text};
  identifier: t=( $IDENTIFIER
                | lastdot $BEGIN..$Yield
                ) ~(nosp $Colon) {Token(t).text};
  unquote: $Backtick e=expr $Backtick -> ^(Unquote e);
  identifier2: t=($IDENTIFIER|$CONSTANT|$BEGIN..$Yield) { Token(t).text };
  identifier3: nosp t=($InstVar|$ClassVar|$IDENTIFIER|$CONSTANT|$BEGIN..$Yield) { Token(t).text };
  cvar: t=$ClassVar {Object(Token(t).text.substring(2))}
      | $ClassVarBacktick e=expr $Backtick -> ^(Unquote e);
  ivar: t=$InstVar {Object(Token(t).text.substring(1))}
      | $InstVarBacktick e=expr $Backtick -> ^(Unquote e);
  fid: t=$FID {Token(t).text};
  $Memo
  fname: ($FID)=> fid
       | ($Backtick)=> unquote
       | ( n=identifier2 t=( nosp $EQ {"="}
                           | {""}
                           )
           {"#{n}#{t}"}
         | op
         );

  annotation scope(@BEG): $Dollar n=$CONSTANT
              arg=(($LBrack)=> $LBrack opt_nl ( e=expr opt_nl $RBrack -> ^(Hash ^(Assoc ^(Symbol "value") e))
                                              | a=listOf(:assoc) trailer opt_nl $RBrack -> ^(Hash a+)
                                              | $RBrack {nil}
                                              )
                  | {nil} )
              name={Token(n).text}
              -> ^(Annotation name arg);

  op: $LT {"<"}
    | $LE {"<="}
    | $LEG {"<=>"}
    | $GT {">"}
    | $GE {">="}
    | $LShift {"<<"}
    | $LLShift {"<<<"}
    | $RShift {">>"}
    | $Bang {"!"}
    | $NE {"!="}
    | $NMatch {"!~"}
    | $Slash {"/"}
    | $Pipe {"|"}
    | $Amper {"&"}
    | $Star {"*"}
    | $Stars {"**"}
    | $EEQ {"=="}
    | $EEEQ {"==="}
    | $Match {"=~"}
    | $LBrack nosp $RBrack (nosp $EQ {"[]="}
                           | {"[]"})
    | $Plus (nosp $At {"+@"}
            | {"+"})
    | $Minus (nosp $At {"-@"}
            | {"-"})
    | $Caret {"^"}
    | $Percent {"%"}
    | $Tilde {"~"}
    ;

  $Memo[Ast]
  cname: c=constant -> ^(Constant c)
       | unquote
       | &identifier !"class/module name must be CONSTANT" ?{false};

  # JRuby allows primary_value in cpath. Do we want that?
  cpath: colon3 n=cname -> ^(Colon3 n)
       | a=(n=cname colons {n})* b=cname
         (?{List(a).size == 0} {b} |
          names={a+[b]}-> ^(Colon2 names+));

  superclass: term {Ast(nil)}
            | $LT c=cpath term  # Should this be expr_value instead of cpath?
              {c};

  variable: ($Nil)=> $Nil                             -> ^(Nil)
          | ($Self)=> $Self                           -> ^(Self)
          | ($True)=> $True                           -> ^(True)
          | ($False)=> $False                         -> ^(False)
          | ($ClassVar|$ClassVarBacktick)=> v=cvar    -> ^(ClassVar v)
          | ($InstVar|$InstVarBacktick)=> v=ivar      -> ^(InstVar v)
          | ($Dollar)=> annotation
          | ($FID)=> n=fid                            -> ^(FCall n)
          | ($CONSTANT)=> c=constant                  -> ^(Constant c)
          | ($IDENTIFIER)=> n=identifier              -> ^(Identifier n)
          | (lastdot)=> n=identifier                  -> ^(Identifier n);

  symbol: $Colon n=identifier3 -> ^(Symbol n);

  $Memo[Ast]
  string: character
        | strings=string1+ {stringConcat(strings)};

  string1: $SQuote! s=squote_contents* $SQuote -> ^(String join(s))
         | $DQuote $DQuote -> ^(String "")
         | $DQuote dc=dquote_contents $DQuote {dc}
         | $DQuote! c=(dquote_contents | str_ev2)* $DQuote -> ^(DString c+)
         | x=heredoc_id n= -> ^(Heredoc x) { @heredocs.addLast(n); n }
         ;

  regexp: $RegexBegin c=(regexp_contents | str_ev2)* e=$RegexEnd options={Token(e).text.substring(1)}
          -> ^(Regex c options);

  str_ev: "\#@@"! nosp n=identifier2 -> ^(EvString ^(ClassVar n))
        | "\#@"!  nosp n=identifier2 -> ^(EvString ^(InstVar n))
        | "\#{"! c=compstmt "}" -> ^(EvString c);

  str_ev2: $StrEvBegin c=compstmt $RBrace -> ^(EvString c)
         | $StrEvBegin cv=cvar -> ^(EvString ^(ClassVar cv))
         | $StrEvBegin iv=ivar -> ^(EvString ^(InstVar iv))
         ;

  heredoc_id: $HereDocId;

  squote_contents: e=$Escape {Token(e).text.substring(1)}
                 | s=$StringContent {Token(s).text}
                 ;
  dquote_contents: c=(t=$Escape e={parse_escape(Token(t).text)}
                     |t=$StringContent {Token(t).text}
                     )+ -> ^(String join(c));
  regexp_contents: c=(t=$Escape e={parse_escape(Token(t).text, 'regex')}
                     |t=$StringContent {Token(t).text}
                     )+ -> ^(String join(c));

  squote_heredoc: s=(t=$StringContent {Token(t).text})* heredoc_marker {join(s)};
  dquote_heredoc: n=( str_ev
                    | t=$StringContent s={Token(t).text} -> ^(String s)
                    )* heredoc_marker {n};

  heredoc_marker: $HereDocEnd (nl | end);

  character: t=$Character
             e={parse_escape(Token(t).text.substring(1))}
             -> ^(Character getCodePoint(e));

  escape: unicode_escape
        | "\\"! ("n" {"\n"}
                |"s" {"\s"}
                |"r" {"\r"}
                |"t" {"\t"}
                |"v" {"\v"}
                |"f" {"\f"}
                |"b" {"\b"}
                |"a" {"\a"}
                |"e" {"\e"}
                |"\n" {""}
                |codepoint
                );

  unicode_escape: "\\" ( "\n" {""}
                       | "\\" {"\\"}
                       | a=oneOf(@OCTAL) b=oneOf(@OCTAL) c=oneOf(@OCTAL)
                         {String.new(Character.toChars(Integer.parseInt("#{a}#{b}#{c}", 8)))}
                       |"x"! a=oneOf(@HEX) b=oneOf(@HEX)
                         {String.new(Character.toChars(Integer.parseInt("#{a}#{b}", 16)))}
                       |"u"! c1=oneOf(@HEX) c2=oneOf(@HEX) c3=oneOf(@HEX) c4=oneOf(@HEX)
                         {String.new(Character.toChars(Integer.parseInt("#{c1}#{c2}#{c3}#{c4}", 16)))}
                       |"U"! c1=oneOf(@HEX) c2=oneOf(@HEX) c3=oneOf(@HEX) c4=oneOf(@HEX)
                             c5=oneOf(@HEX) c6=oneOf(@HEX) c7=oneOf(@HEX) c8=oneOf(@HEX)
                         {String.new(Character.toChars(Integer.parseInt("#{c1}#{c2}#{c3}#{c4}#{c5}#{c6}#{c7}#{c8}", 16)))}
                       );

  Then: term? $Then | term;
  Ensure: $Ensure! compstmt;
  Else: $Else! compstmt;
  Do: ($Do | term) UNCOND;

  if_tail: Else
         | $Elsif! opt_nl cond=value(expr) Then body=compstmt tail=if_tail
           -> ^(If cond body tail)
         | {nil};

  Rescue: ($Rescue! c=listOf(:constant) ($Rocket opt_nl n=identifier)?
           Then b=compstmt -> ^(RescueClause c n b))+;

  fixnum: t=$Integer n={ parseInt(Token(t).text) } -> ^(Fixnum n);

  floatLiteral: t=$Float f={ parseFloat(Token(t).text) } -> ^(Float f);

  lastdot: ?{_rpeek.indexOf(".") == 0};

  op_attr_assign target=_ attr=_ args=_ op=_ value=_
               : target2=-> ^(LocalAssign temp target)
                 args2=save_temps(args)
                 assignment=op_attr_assign_body(op, target2, attr, args2, value)
                 l={[target2] + args2 + [assignment]}
                 -> ^(Body l+);

  op_attr_assign_body op=:'&&' target=_ attr=_ args=_ value=_
      : -> ^(And ^(Call attr gettable2(target) get_temps(args))
                 ^(AttrAssign "#{attr}=" gettable2(target) get_temps(args) value)
            );
  op_attr_assign_body op=:'||' target=_ attr=_ args=_ value=_
      : -> ^(Or ^(Call attr gettable2(target) get_temps(args))
                ^(AttrAssign "#{attr}=" gettable2(target) get_temps(args) value)
            );
  op_attr_assign_body op=_ target=_ attr=_ args=_ value=_
      : -> ^(AttrAssign "#{attr}=" gettable2(target) get_temps(args)
                        ^(Call op ^(Call attr gettable2(target) get_temps(args))
                                  value
                         )
            );

  assign_temp value=_ : -> ^(LocalAssign temp value);

  block_start token=_ : {pos(Token(token).startpos)};
  verify_end start=_ : ( t=&$End {verify_columns(start, pos(Token(t).startpos));nil}
                       | {nil});

  $Memo[Token]
  lex: {@lexer.lex(_pos)};

  def self.parse_args(args:String[]) {
    argc = args.length
    if args[0].equals "-v"
      BaseParser.tracing = true
      argc -= 1
    elsif args[0].equals "--tree"
      BaseParser.debug_parse_tree = true
      argc -= 1
    end
    if argc > 0
      filename = args[args.length - 1]
      stream = InputStream(FileInputStream.new(filename))
    else
      stream = System.in
    end
    reader = BufferedReader.new(InputStreamReader.new(stream))
    buffer = char[8192]
    builder = StringBuilder.new
    while (read = reader.read(buffer, 0, buffer.length)) > 0
      builder.append(buffer, 0, read);
    end
    return builder.toString
  }

  def self.main(args:String[]) {
      returns void
      parser = MirahParser.new
      code = parse_args(args)
      begin
        result = parser.parse(code)
        puts BaseParser.print_r(result) unless BaseParser.debug_parse_tree
      ensure
        unless BaseParser.debug_parse_tree
          mb = 1024 * 1024
          runtime = Runtime.getRuntime
          puts "##### Heap utilization statistics [MB] #####"
          puts "Used Memory: #{(runtime.totalMemory - runtime.freeMemory) / mb}"
          puts "Total Memory: #{runtime.totalMemory / mb}"
          puts "Free Memory: #{runtime.freeMemory / mb}"
          puts "Max Memory: #{runtime.maxMemory / mb}"
        end
      end
  }

  def initialize {
    @BEG = 0
    @begchars = " ([{,;=" #}
    @HEX = "0123456789abcdefABCDEF"
    @DECIMAL = "0123456789"
    @OCTAL = "01234567"
    @BINARY = "01"
    @reserved = EnumSet.range(Tokens.tBEGIN, Tokens.tYield)
    @heredocs = LinkedList.new
    @nil = Ast.new('Nil')
    @nil.start_position = @nil.end_position = Position.new('', 0, 0, 0)
    @lhs_map = {
      "ConstAssign" => "Constant",
      "LocalAssign" => "Local",
      "InstVarAssign" => "InstVar",
      "ClassVarAssign" => "ClassVar",
    }
    @escape = EscapeParser.new
  }

  def init {
    @lexer = MirahLexer.new(_string, @chars, self)
    @temps = 0
  }

  def temp {
    "$ptemp#{@temps += 1}"
  }

  def save_temps {
    items = List(_pop)
    temps = ArrayList.new
    items.each do |item|
      _push(item)
      temps.add(assign_temp)
    end
    temps
  }

  def get_temps(_items:Object) {
    items = List(_items)
    gets = ArrayList.new
    items.each do |item|
      gets.add(gettable2(item))
    end
    gets
  }

  def parse_escape(character:String, rule:String=nil) {
    if character.startsWith('\\')
      begin
        String(@escape.parse(character, rule))
      rescue SyntaxError
        raise SyntaxError.new("valid escape", "", _pos, _string, _list);
      end
    else
      character
    end
  }

  def oneOf() {
    chars = String(_pop)
    _char(chars)
  }

  def _codepoint() {
    begin
      t = _any
    rescue RuleFailure
      return (-1)
    end
    character = Character(t).charValue
    if character >= 0xd800 && character <= 0xdbff
      begin
        t = _any
      rescue RuleFailure
        return (-1)
      end
      return Character.toCodePoint(character, Character(t).charValue)
    else
      return int(character)
    end
  }

  def codepoint {
    start = _pos
    t = _codepoint
    if t == -1
      self._pos = start
      raise @error
    else
      return String.new(Character.toChars(t))
    end
  }

  def identifierChar() {
    start = _pos
    c = _codepoint
    if c == 95 || Character.isLetterOrDigit(c)
      String.new(Character.toChars(c))
    else
      self._pos = start
      raise @error
    end
  }

  heredoc: $HereDocBegin!
           n={Ast(@heredocs.removeFirst)}
           c=heredoc1
           heredoc_marker
           {n.name = c.name;n.children.clear;n.children.addAll(c.children); "\n"}
         ;
  heredoc1  scope(@heredocs): {@heredocs = LinkedList.new} heredoc2;
  heredoc2:  t=$StringContent s={Token(t).text} &$HereDocEnd -> ^(String s)
          | c=(str_ev | t=$StringContent s={Token(t).text} -> ^(String s))+
            -> ^(DString c*)
          | /* empty */ -> ^(String "")
          ;

  def parseInt(string:String) {
    radix = 10
    if string.length > 1 && string.charAt(0) == 48 # '0'
      type = string.toLowerCase.substring(1, 2)
      if 'x'.equals(type)
        radix = 16
        string = string.substring(2)
      elsif 'o'.equals(type)
        radix = 8
        string = string.substring(2)
      elsif 'b'.equals(type)
        radix = 2
        string = string.substring(2)
      elsif 'd'.equals(type)
        string = string.substring(2)
      else
        radix = 8
      end
    end
    begin
      Long.valueOf(Long.parseLong(string.replace('_', ''), radix))
    rescue NumberFormatException
      raise SyntaxError.new('valid number', '', _pos, _string, _list)
    end
  }

  def parseFloat(string:String) {
    Double.new(string.replace('_', ''))
  }

  def stringConcat(strings:List) {
    result = Object(nil)
    List(strings).each do |s|
      result = stringConcat(result, s)
    end
    result
  }

  def stringConcat(a:Object, b:Object) {
    la = Ast(a)
    lb = Ast(b)
    return la if lb.nil?
    return lb if la.nil?
    result = Ast.new("DString")
    [la, lb].each do |n|
      node = Ast(n)
      if node.name.equals("DString")
        result.addAll(node.children)
      else
        result.add(node)
      end
    end
    result.start_position = la.start_position
    result.end_position = lb.end_position
    result
  }

  def getCodePoint(c:Object) {
    String(c).codePointAt(0)
  }

  def obj2ast(x:Object) {
    returns Ast
    if x.kind_of?(Ast)
      Ast(x)
    elsif x.kind_of?(List)
      l = List(x)
      if l.size == 0
        Ast(nil)
      elsif l.size == 1
        Ast(obj2ast(l.get(0)))
      else
        ast = Ast.new('Body', l)
        first = obj2ast(l.get(0))
        last = obj2ast(l.get(l.size - 1))
        ast.start_position = first.start_position
        ast.end_position = last.end_position
        ast
      end
    elsif x.nil?
      Ast(nil)
    else
      raise "Expected Ast, got #{x} (#{x.getClass})"
    end
  }

  def addToBlock(a:Object, b:Object) {
    returns Ast
    x = obj2ast(a)
    y = obj2ast(b)
    if x.nil?
      return y
    elsif y.nil?
      return x
    end
    x_kids = 'Body'.equals(x.name) ? x.children : [x]
    y_kids = 'Body'.equals(y.name) ? y.children : [y]
    body = Ast.new('Body', x_kids)
    body.addAll(y_kids)
    body.start_position = x.start_position
    body.end_position = y.end_position
    body
  }

  def checkExpression(e:Object) {
    # TODO actually check
    Ast(e || @nil)
  }

  def gettable2(e:Object) {
    n = Ast(e)
    n2 = Ast(n.clone)
    n2.name = String(@lhs_map.get(n.name))
    n2.remove(2) if n2.size == 3
    n2
  }

  def negateFixnum(f:Object) {
    n = Number(Ast(f).get(1)).longValue
    Long.valueOf(-n)
  }

  def negateFloat(f:Object) {
    d = Double(Ast(f).get(1)).doubleValue
    Double.valueOf(-d)
  }

  def _memo_key(s:String) {
    "#{s}:#{@BEG}:#{@cond}"
  }

  def verify_columns(_start:Object, _end:Object) {
    start_pos = Position(_start)
    end_pos = Position(_end)
    if end_pos.col != start_pos.col
      if start_of_line(start_pos) && start_of_line(end_pos)
        warn("Misaligned end", "closes block", start_pos.pos)
      end
    end
  }

  def start_of_line(position:Position) {
    1.upto(position.col - 1) do |n|
      c = _string.charAt(position.pos - n)
      if " \t\f\r\v".indexOf(c) == -1
        return false
      end
    end
    return true
  }
}

parser EscapeParser {
  def initialize {
    @HEX = "0123456789abcdefABCDEF"
    @OCTAL = "01234567"
  }

  def oneOf() {
    chars = String(_pop)
    _char(chars)
  }

  start: escape;
  unicode_escape: "\\" ( "\n" {""}
                       | "\\" {"\\"}
                       | a=oneOf(@OCTAL) b=oneOf(@OCTAL) c=oneOf(@OCTAL)
                         {String.new(Character.toChars(Integer.parseInt("#{a}#{b}#{c}", 8)))}
                       |"x"! a=oneOf(@HEX) b=oneOf(@HEX)
                         {String.new(Character.toChars(Integer.parseInt("#{a}#{b}", 16)))}
                       |"u"! c1=oneOf(@HEX) c2=oneOf(@HEX) c3=oneOf(@HEX) c4=oneOf(@HEX)
                         {String.new(Character.toChars(Integer.parseInt("#{c1}#{c2}#{c3}#{c4}", 16)))}
                       |"U"! c1=oneOf(@HEX) c2=oneOf(@HEX) c3=oneOf(@HEX) c4=oneOf(@HEX)
                             c5=oneOf(@HEX) c6=oneOf(@HEX) c7=oneOf(@HEX) c8=oneOf(@HEX)
                         {String.new(Character.toChars(Integer.parseInt("#{c1}#{c2}#{c3}#{c4}#{c5}#{c6}#{c7}#{c8}", 16)))}
                       );

  escape: unicode_escape
        | "\\"! ("n" {"\n"}
                |"s" {"\s"}
                |"r" {"\r"}
                |"t" {"\t"}
                |"v" {"\v"}
                |"f" {"\f"}
                |"b" {"\b"}
                |"a" {"\a"}
                |"e" {"\e"}
                |"\n" {""}
                |codepoint
                );

  regex: unicode_escape
       | "\\"! c=codepoint {"\\#{c}"};

  def _codepoint() {
    begin
      t = _any
    rescue RuleFailure
      return (-1)
    end
    character = Character(t).charValue
    if character >= 0xd800 && character <= 0xdbff
      begin
        t = _any
      rescue RuleFailure
        return (-1)
      end
      return Character.toCodePoint(character, Character(t).charValue)
    else
      return int(character)
    end
  }

  def codepoint {
    start = _pos
    t = _codepoint
    if t == -1
      self._pos = start
      raise @error
    else
      return String.new(Character.toChars(t))
    end
  }
}